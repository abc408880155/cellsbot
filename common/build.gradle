
plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
}

archivesBaseName = 'cellsbot-common'

repositories {
    mavenCentral()
}

dependencies {
    api(platform("net.mamoe:mirai-bom:2.15.0-M1"))
    api("net.mamoe:mirai-core-api")     // 编译代码使用
    runtimeOnly("net.mamoe:mirai-core") // 运行时使用

    api 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    api 'org.jetbrains:annotations:24.0.1'
    api'com.google.guava:guava:31.1-jre'
    api 'commons-lang:commons-lang:2.6'
    // need 1.9
    api 'org.yaml:snakeyaml:1.9'
    api 'ch.qos.logback:logback-classic:1.4.6'
    api 'ch.qos.logback:logback-core:1.4.6'

}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "16"
}

jar {
    from {
        // common模块中包含了重复的module-info.class文件，需要设置一个解决重复文件的策略。
        // 可能是因为Mirai库自身包含了一个module-info.class文件。
        // 而common模块也声明了一个module-info.java文件，因此出现了冲突。
        // duplicatesStrategy选项设置为'exclude'，表示当出现重复文件时，Gradle将自动忽略其中一个文件
        duplicatesStrategy = 'exclude'
        exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF',
                'AUTHORS', 'changelog.*', 'LICENSE*', 'module-info.class', 'README*', 'release-timestamp.*'
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
